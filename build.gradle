plugins {
    id "de.dfki.mary.lexicon-compiler" version "0.3.0"
    id 'maven-publish'
}

group 'de.dfki.mary'
version '0.2.0-SNAPSHOT'
description 'Luxembourgish lexicon for MaryTTS'

ext {
    url = 'https://github.com/marytts/marytts-lexicon-lb'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

compileLexicon {
    phoneMapping = [
            'A~:'  : 'a~',
            'J'    : 'j',
            's\\\\': 'S',
            'z\\\\': 'Z'
    ]
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = project.url
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 only'
                        url = 'https://spdx.org/licenses/LGPL-3.0-only.html'
                    }
                }
                developers {
                    developer {
                        name = 'Ingmar Steiner'
                        id = 'psibre'
                    }
                    developer {
                        name = 'Peter Gilles'
                        id = 'PeterGilles'
                    }
                }
                contributors {
                    contributor {
                        name = 'Sascha Brawer'
                        url = 'https://github.com/brawer'
                    }
                }
                scm {
                    connection = "scm:git:${project.url}.git"
                    developerConnection = "scm:git:ssh://git@github.com:marytts/${project.name}.git"
                    url = project.url
                }
            }
        }
    }
    repositories {
        maven {
            url "$rootProject.buildDir/repo"
        }
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/marytts/marytts/marytts'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}
